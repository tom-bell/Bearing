buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.0'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
    }
}

repositories {
    mavenCentral()
}

apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.library'
apply from: 'gradle/sonatype.gradle'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 22
        versionName version
        versionCode versionCode
    }
}

android.libraryVariants.all { variant ->
    def variantName = variant.name.capitalize();
    task("generate${variantName}Javadoc", type: Javadoc) {
        description "Generates Javadoc for the ${variantName} build"
        source = variant.javaCompile.source
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files, ext.androidJar)

        failOnError false        

        options {
            links "http://docs.oracle.com/javase/7/docs/api/"
            linksOffline "http://d.android.com/reference", "${android.sdkDirectory}/docs/reference"
            linksOffline "http://d.android.com/reference", "${android.sdkDirectory}/extras/google/google_play_services/docs/reference"
        }
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
    }
//    tasks.androidJavadocsJar.dependsOn "generate${variantName}Javadoc"
    tasks.getByName("generate${variantName}Javadoc").dependsOn "assemble${variantName}"
}

dependencies {
    // Explicit dependency on support lib to allow sdk-manager plugin to prepare it
    compile 'com.android.support:support-v4:22.0.0'
    compile 'com.google.android.gms:play-services:6.5.87'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.0.5-beta'
}
